// Copyright 2021 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Local register allocator that spills everything to the stack between basic blocks.
def verbose = true;
def paranoid = true;
class LocalRegAlloc(regSet: MachRegSet, codegen: SsaMachGen) {
	def regState = Array<int>.new(regSet.physRegs);			// indices into {allocated}
	def allocated = Array<(VReg, int)>.new(regSet.physRegs);	// (VReg, pos)
	def parmoveDests = Vector<(VReg, List<Operand.Def>)>.new();
	def moveResolver = MoveResolver.new(codegen.mach.prog.ERROR);
	def moves = MoveSet.new(regSet);
	var cursor: int;
	var pos: int;
	var buf = ArchInstrBuffer.new(codegen, codegen.mach.prog, regSet);
	var prev: ArchInstr;
	var cur: ArchInstr;
	var next: ArchInstr;

	def allocate() {
		clear();
		cur = codegen.last;
		while (cur != null) {
			if (verbose) dumpState("before instr");
			if (verbose) Terminal.put(buf.putInstrV(cur).toString());
			if (paranoid) sanityCheck();
			pos++;
			prev = cur.prev;
			next = cur.next;
			var opcode = cur.opcode();
			if (opcode == ArchInstrs.ARCH_PARMOVE) { // perform parallel move
				cur.remove();
				parmoveDests.resize(0);
				codegen.gatherParallelMoveDests(cur, parmoveDests);
				for (j < parmoveDests.length) recursivelyEmitMoves(j, next);
				cur = prev;
				continue;
			}
			if (opcode == ArchInstrs.ARCH_BLOCK) {
				var block = Operand.Label.!(cur.operands[0]).block;
				if (block.preds.length != 0) {  // not the entry block
					for (i < cursor) { // Load all live registers out of their spill slots
						var a = allocated[i];
						var vreg = a.0;
						regState[vreg.reg] = -1;
						allocSpill(vreg);
						codegen.insertRestoreLocal(vreg, vreg.reg, next);
						vreg.reg = 0;
					}
					cursor = 0;
				}
				cur = prev;
				continue;
			}
			if (opcode == ArchInstrs.ARCH_BLOCK_END) {
				cur = prev;
				continue;
			}
			var operands = cur.operands;
			// process defs and kills
			moves.clear();
			// first pass on defs; free registers of anything defined in this instruction, but don't clear vreg.reg
			for (j < operands.length) match (operands[j]) {
				Def(vreg, constraint) 			=> freeReg(vreg.reg, false);
				Overwrite(vreg, use, constraint) 	=> freeReg(vreg.reg, false);
				_ => ;
			}
			// second pass on defs; allocate (new) registers and shuffle into previous vreg.reg
			for (j < operands.length) match (operands[j]) {
				Def(vreg, constraint) => {
					var loc = allocDefReg(vreg, constraint);
					operands[j] = Operand.Def(vreg, loc);
				}
				Overwrite(vreg, use, constraint) => {
					var loc = allocDefReg(vreg, constraint);
					operands[j] = Operand.Overwrite(vreg, use, loc);
				}
				_ => ;
			}
			pos++;
			if (verbose) dumpState("after defs  ");
			// process uses
			for (j < operands.length) match (operands[j]) {
				Def(vreg, assignment) => {
					if (paranoid) sanityCheck();
					freeReg(assignment, true);
					if (paranoid) sanityCheck();
				}
				Overwrite(dst, use, assignment) => {
					if (paranoid) sanityCheck();
					freeReg(assignment, true);
					if (paranoid) sanityCheck();
					if (use.reg != 0) { // TODO: live in
						codegen.insertMoveLocLoc((use, use.reg), (use, assignment), use.regClass, cur);
					} else {
						allocReg(use, assignment);
					}
					if (paranoid) sanityCheck();
				}
				Kill(constraint) => {
					if (paranoid) sanityCheck();
					if (constraint < regSet.regSets.length) {
						for (reg in regSet.regSets[constraint]) {
							spillReg(reg, next);
							freeReg(reg, true);
						}
					}
					if (paranoid) sanityCheck();
				}
				Use(vreg, constraint) => {
					if (paranoid) sanityCheck();
					var loc: int = vreg.reg;
					if (!isOk(vreg, constraint)) {
						spillReg(vreg.reg, next);
						loc = allocReg(vreg, findBestLoc(vreg.regClass, vreg.hint, constraint));
					}
					operands[j] = Operand.Use(vreg, loc);
					if (paranoid) sanityCheck();
				}
				_ => ;
			}
			moves.emit(codegen, cur.next);
			if (opcode == ArchInstrs.ARCH_ENTRY) {
				for (j < cursor) {
					var vreg = allocated[j].0;
					if (vreg.isConst()) codegen.insertMoveValLoc(vreg, (vreg, vreg.reg), vreg.regClass, next);
				}
			}
			cur = prev;
		}
	}
	def allocDefReg(vreg: VReg, constraint: int) -> int {
		var prevReg = vreg.reg;
		vreg.reg = 0;
		var loc = allocReg(vreg, findBestLoc(vreg.regClass, prevReg, constraint));
		if (prevReg != 0) moves.addDef(vreg, loc, prevReg);
		if (vreg.spill != 0) moves.addDef(vreg, loc, vreg.spill);
		return loc;
	}
	def dumpState(str: string) {
		buf.reset();
		buf.puts(str);
		buf.puts(" regState = [");
		for (j < regState.length) {
			var i = regState[j];
			if (i < 0) continue;
			var a = allocated[i];
			buf.puts(regSet.identify(j)).putc('=');
			buf.putVReg("", a.0);
			buf.putc('^');
			buf.putd(a.1);
			buf.csp();
		}
		Terminal.put(buf.puts("]").ln().toString());
	}
	def spillReg(reg: byte, next: ArchInstr) {
		var i = regState[reg];
		if (i < 0) return;
		var vreg = allocated[i].0;
		allocSpill(vreg);
		codegen.insertRestoreLocal(vreg, reg, next);
	}
	def allocSpill(vreg: VReg) {
		if (vreg.spill == 0) vreg.spill = codegen.frame.allocSpill(vreg.regClass);
	}
	def freeReg(loc: int, clear: bool) {
		if (loc >= regState.length) return;
		var i = regState[loc];
		if (i < 0) return;
		var o = allocated[i];
		var a = allocated[--cursor];
		if (cursor > 0) { 
			allocated[i] = a;
			regState[a.0.reg] = i;
		}
		regState[loc] = -1;
		if (clear) o.0.reg = 0;
	}
	def isOk(vreg: VReg, constraint: int) -> bool {
		if (vreg.reg == 0) return false;
		if (constraint >= regSet.regSets.length) return false;
		if (constraint == 0) return true;
		return regSet.isInRegSet(vreg.reg, constraint);
	}
	def findBestLoc(regClass: RegClass, hint: int, constraint: int) -> int {
		if (constraint >= regSet.regSets.length) return constraint; // constraint is for spill slot
		if (constraint == 0) constraint = regSet.regClasses[regClass.tag];
		if (hint != 0) { // try hint register first
			var i = regState[hint];
			if (i < 0 && regSet.isInRegSet(hint, constraint)) return hint; // hint register is free
		}
		// Find a register from the constraint set; a free on, or the one used farthest in future
		var minpos = int.max, newReg: byte = 0;
		for (reg in regSet.regSets[constraint]) {
			var i = regState[reg];
			if (i < 0) return reg; // found a free register in the set
			var a = allocated[i];
			if (a.1 < minpos) {
				minpos = a.1;
				newReg = reg;
			}
		}
		if (newReg == 0) codegen.context.fail("no registers available");
		return newReg;
	}
	def allocReg(vreg: VReg, reg: int) -> int {
		if (reg > regSet.physRegs) return reg;
		var i = regState[reg];
		if (i < 0) {
			regState[reg] = cursor;
			allocated[cursor++] = (vreg, pos);
		} else {
			var old = allocated[i].0;
			allocated[i] = (vreg, pos);
			allocSpill(old);
			moves.reloads.put(old, reg);
			old.reg = 0;
		}
		return vreg.reg = byte.!(reg);
	}
	def sanityCheck() {
//		dumpState("sanity");
		if (regState[0] >= 0) return V3.fail("regState[unassigned] is allocated");
		for (j < regState.length) {
			var i = regState[j];
			if (i < 0) continue;
			if (i >= cursor) return V3.fail1("regState[%s] index oob", regSet.identify(j));
			var a = allocated[i], vreg = a.0;
			if (vreg == null) return V3.fail1("allocated[%d] unallocated", i);
			if (vreg.reg != j) return V3.fail(Strings.format2("v%d.reg != %s", vreg.varNum, regSet.identify(j)));
		}
		for (i < cursor) {
			var a = allocated[i], vreg = a.0;
			if (vreg == null) return V3.fail1("allocated[%d] unallocated", i);
			if (vreg.reg <= 0) return V3.fail(Strings.format2("allocated[%d]=v%d mismatch", i, vreg.varNum));
			if (vreg.reg >= allocated.length) return V3.fail1("allocated[%d] mismatch", i);
			if (a.1 > pos) return V3.fail1("allocated[%d] pos too big", i);
		}
		for (j < codegen.vars.length) {
			if (true) continue; // TODO
			var vreg = codegen.vars[j];
			if (vreg == null) continue;
			if (vreg.reg != 0) {
				var i = regState[vreg.reg];
				if (i < 0) return V3.fail(Strings.format2("regState[@%d, %s] mismatched", j, regSet.identify(vreg.reg)));
				if (i >= cursor) return V3.fail(Strings.format2("regState[@%d, %s] mismatched", j, regSet.identify(vreg.reg)));
				var a = allocated[i];
				if (a.0 != vreg) return V3.fail(Strings.format2("regState[@%d, %s] mismatched", j, regSet.identify(vreg.reg)));
			}
		}
	}
	def clear() {
		for (i < allocated.length) allocated[i] = (null, -1);
		for (i < regState.length) regState[i] = -1;
		cursor = 0;
	}
	def recursivelyEmitMoves(j: int, next: ArchInstr) {
		var t = parmoveDests[j], vreg = t.0, dest = t.1;
		if (vreg.parmoveState <= 0) return; // already done on or stack
		vreg.parmoveState = ON_STACK;
		for (l = dest; l != null; l = l.tail) {
			var o = l.head, dreg = o.vreg, index = dreg.parmoveState;
			allocSpill(dreg);
			if (index == ON_STACK) {
				// destination is already on stack; cycle; load to temp
				dreg.parmoveState = IN_CYCLE;
				codegen.insertRestoreLocal(dreg, getParallelMoveReg(dreg.regClass, 1), next);
			}
			if (index > 0) recursivelyEmitMoves(index - 1, next);
		}
		allocSpill(vreg);
		var loc: int;
		if (vreg.parmoveState == IN_CYCLE) {
			loc = getParallelMoveReg(vreg.regClass, 1);
		} else {
			loc = getParallelMoveReg(vreg.regClass, 0);
			codegen.insertRestoreLocal(vreg, loc, next);
		}
		for (l = dest; l != null; l = l.tail) {
			codegen.insertSaveLocal(loc, l.head.vreg, next);
		}
		vreg.parmoveState = 0;
	}
	def getParallelMoveReg(rcl: RegClass, which: int) -> int {
		var set = regSet.regClasses[rcl.tag];
		var loc = regSet.regSets[set][which];
		return loc;
	}
}
def LIVE: byte = 3;
def ON_STACK: byte = 2;
def IN_CYCLE: byte = 1;

class MoveSet(regSet: MachRegSet) {
	def entries = Array<(byte, VReg, int)>.new(regSet.physRegs);
	def saves = Vector<(VReg, int, int)>.new();
	def reloads = Vector<(VReg, int)>.new();

	def addDef(vreg: VReg, src: int, dst: int) {
		if (src != dst)	{
			if (regSet.isReg(dst)) entries[src] = (LIVE, vreg, dst);
			else saves.put(vreg, src, dst);
		}
	}
	def emit(codegen: SsaMachGen, next: ArchInstr) {
		for (j < saves.length) {
			var s = saves[j], vreg = s.0, src = s.1, dst = s.2;
			codegen.insertMoveLocLoc((vreg, src), (vreg, dst), vreg.regClass, next);
		}
		for (j < entries.length) emit0(codegen, j, next);
		for (j < reloads.length) {
			var t = reloads[j], vreg = t.0;
			if (vreg.isConst()) codegen.insertMoveValLoc(vreg, (vreg, t.1), vreg.regClass, next);
			else codegen.insertMoveLocLoc((vreg, vreg.spill), (vreg, t.1), vreg.regClass, next);
		}
	}
	def emit0(codegen: SsaMachGen, j: int, next: ArchInstr) {
		var t = entries[j], state = t.0, vreg = t.1, dst = t.2;
		if (state < LIVE) return; // already done on or stack
		entries[j] = (ON_STACK, vreg, dst);
		var u = entries[dst];
		if (u.0 == ON_STACK) {
			var scratch = regSet.scratch[vreg.regClass.tag];
			entries[dst] = (IN_CYCLE, vreg, scratch);
			codegen.insertMoveLocLoc((vreg, dst), (vreg, scratch), vreg.regClass, next);
		} else if (u.0 == LIVE) {
			emit0(codegen, dst, next);
		}
		var src = j;
		t = entries[j];
		if (t.0 == IN_CYCLE) src = t.2;
		codegen.insertMoveLocLoc((vreg, src), (vreg, dst), vreg.regClass, next);
		entries[j] = (0, null, 0);
	}
	def clear() {
		for (i < entries.length) entries[i] = (0, null, 0);
		saves.resize(0);
		reloads.resize(0);
	}
}